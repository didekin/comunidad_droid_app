apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.didekindroid"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 6
        versionName "a-0.6.10"
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
        resConfigs "es" /* , "en"  Añadir nuevos idiomas según hagan falta.*/
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }

    testOptions {
        if (rootProject.orchestrator) {
            execution "ANDROID_TEST_ORCHESTRATOR"
        }
    }

    signingConfigs {
        release {
            if (project.hasProperty('keyalias')) {
                keyAlias keyalias
            }
            if (project.hasProperty('keypassword')) {
                keyPassword keypassword
            }
            if (project.hasProperty('keystore')) {
                storeFile file(keystore)
            }
            if (project.hasProperty('storepassword')) {
                storePassword storepassword
            }
        }
    }

    sourceSets {
        debugPre {
            /*manifest.srcFile 'src/debug/AndroidManifest.xml'*/
            java.srcDirs = ['src/debug/java']
            res.srcDirs = ['src/debug/res']
        }
    }

    // Descomentar antes de pasar a PRE.
//    testBuildType "debugPre"

    buildTypes {
        release {
            applicationIdSuffix ""
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "didekin_web_host", '"https://didekin-web-pro.eu-west-1.elasticbeanstalk.com"'
            buildConfigField "String", "didekin_web_port", '":443"'
        }
        pre {
            initWith release
            buildConfigField "String", "didekin_web_host", '"https://didekin-web-pre.eu-west-1.elasticbeanstalk.com"'
            buildConfigField "String", "didekin_web_port", '":443"'
        }
        local {
            initWith release
            buildConfigField "String", "didekin_web_host", '"https://didekinspring.pagekite.me"'
            buildConfigField "String", "didekin_web_port", '""'
        }
        debugPre {
            initWith debug
            // To be used in DidekinApp.
            buildConfigField "String", "didekin_web_host", '"https://didekin-web-pre.eu-west-1.elasticbeanstalk.com"'
            buildConfigField "String", "didekin_web_port", '":443"'
        }
        debug {
            debuggable true
            versionNameSuffix ''
            // To be used in DidekinApp.
            buildConfigField "String", "didekin_web_host", '"https://didekinspring.pagekite.me"'
            buildConfigField "String", "didekin_web_port", '""'
        }
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError true
        quiet true
        ignoreWarnings false
        htmlReport true
    }
    productFlavors {
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        pickFirst 'protobuf.meta'
    }
}

dependencies {
    implementation rootProject.ext.firebase_msg
    implementation rootProject.ext.didekinlib_model
    implementation rootProject.ext.didekinlib_utilities
    implementation rootProject.ext.timber
    runtimeOnly rootProject.ext.retrofit2
    implementation rootProject.ext.rxjava
    implementation rootProject.ext.android_support
    releaseImplementation rootProject.didekindroidlib_one_release
    debugImplementation rootProject.ext.didekindroidlib_one_debug
    debugPreImplementation rootProject.ext.didekindroidlib_one_debug
    androidTestImplementation rootProject.ext.testRuler
    androidTestImplementation rootProject.ext.testRunner
    androidTestImplementation rootProject.ext.espresso
    androidTestImplementation rootProject.ext.awaitility
    androidTestUtil rootProject.ext.testOrchestrator
    testImplementation rootProject.ext.junit
    androidTestImplementation rootProject.ext.junit
}

apply plugin: 'com.google.gms.google-services'


