apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.didekindroid"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 5
        versionName "a-0.5.1"
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
        resConfigs "es" /* Añadir nuevos idiomas según hagan falta.*/
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
    }

    //noinspection GroovyAssignabilityCheck
    signingConfigs {
        release {
            if (project.hasProperty('keyalias')) {
                keyAlias keyalias
            }
            if (project.hasProperty('keypassword')) {
                keyPassword keypassword
            }
            if (project.hasProperty('keystore')) {
                storeFile file(keystore)
            }
            if (project.hasProperty('storepassword')) {
                storePassword storepassword
            }
        }
    }

    buildTypes {
        release {
            applicationIdSuffix ""
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue "string", "didekin_web_host", '"https://didekin-web-pro.eu-central-1.elasticbeanstalk.com"'
            resValue "string", "didekin_web_port", '":443"'
        }
        pre {
            initWith release
            signingConfig signingConfigs.release
            resValue "string", "didekin_web_host", '"https://didekin-web-pre.eu-central-1.elasticbeanstalk.com"'
            resValue "string", "didekin_web_port", '":443"'
        }
        local {
            applicationIdSuffix ""
            debuggable true
            /*shrinkResources true
            minifyEnabled true*/
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue "string", "didekin_web_host", '"https://didekinspring.pagekite.me"'
            resValue "string", "didekin_web_port", '""'
        }
        debug {
            debuggable true
            versionNameSuffix ''
            // Variables for android_studio default run-debug configuration.
            //noinspection GrEqualsBetweenInconvertibleTypes
            if ("$System.env.TEST_HOST" == "null" || "$System.env.TEST_PORT" == "null"){
            // LOCAL url's
//                resValue "string", "didekin_web_host", '"https://didekinspring.pagekite.me"'
//                resValue "string", "didekin_web_port", '""'
            // AWSPRE url's
                resValue "string", "didekin_web_host", '"https://didekin-web-pre.eu-central-1.elasticbeanstalk.com"'
                resValue "string", "didekin_web_port", '":443"'
            } else {
                // Variables defined in buildtest.sh for local and awspre environments.
                resValue "string", "didekin_web_host", "$System.env.TEST_HOST"
                resValue "string", "didekin_web_port", "$System.env.TEST_PORT"
            }
        }
    }

    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "1g"
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError true
        quiet true
        ignoreWarnings false
        htmlReport true
//        disable  "InvalidPackage"
    }
    productFlavors {
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    implementation rootProject.ext.firebase_message
    implementation rootProject.ext.didekinlib_model
    implementation rootProject.ext.didekinlib_http
    implementation rootProject.ext.timber
    runtimeOnly rootProject.ext.retrofit2
    implementation rootProject.ext.rxjava
    implementation rootProject.ext.android_support
    androidTestImplementation rootProject.ext.testRuler
    androidTestImplementation rootProject.ext.espresso
    androidTestImplementation rootProject.ext.didekinlib_gcm
    androidTestImplementation rootProject.ext.awaitility
    androidTestImplementation rootProject.ext.testRunner
    androidTestUtil rootProject.ext.testOrchestrator
    testImplementation rootProject.ext.junit
    androidTestImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
