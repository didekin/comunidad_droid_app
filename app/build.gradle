apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.didekindroid"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 5
        versionName "a-0.4.11"
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
        resConfigs "es" /* Añadir nuevos idiomas según hagan falta.*/
    }
    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    //noinspection GroovyAssignabilityCheck
    signingConfigs {
        release {
            if (project.hasProperty('keyalias')) {
                keyAlias keyalias
            }
            if (project.hasProperty('keypassword')) {
                keyPassword keypassword
            }
            if (project.hasProperty('keystore')) {
                storeFile file(keystore)
            }
            if (project.hasProperty('storepassword')) {
                storePassword storepassword
            }
        }
    }

    buildTypes {
        release {
            applicationIdSuffix ""
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue "string", "didekin_web_host", '"https://didekinspring.pagekite.me"'
            resValue "string", "didekin_web_port", '":443"'
        }
        pre {
            initWith release
            signingConfig signingConfigs.release
            resValue "string", "didekin_web_host", '"https://didekin-web-pre.eu-central-1.elasticbeanstalk.com"'
            resValue "string", "didekin_web_port", '":443"'
        }
        local {
            applicationIdSuffix ""
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue "string", "didekin_web_host", '"https://didekinspring.pagekite.me"'
            resValue "string", "didekin_web_port", '""'
        }
        debug {
            debuggable true
            versionNameSuffix ''
            // Variables defined in buildtest.sh
            resValue "string", "didekin_web_host", "$System.env.TEST_HOST"
            resValue "string", "didekin_web_port", "$System.env.TEST_PORT"
        }
    }

    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "1g"
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError true
        quiet true
        ignoreWarnings false
        htmlReport true
//        disable  "InvalidPackage"
    }
    productFlavors {
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    compile rootProject.ext.firebase_message
    compile rootProject.ext.didekinlib_model
    compile rootProject.ext.didekinlib_http
    compile rootProject.ext.timber
    apk rootProject.ext.retrofit2
    compile rootProject.ext.rxjava
    compile rootProject.ext.android_support

    androidTestCompile rootProject.ext.espresso
    androidTestCompile rootProject.ext.didekinlib_gcm
    androidTestCompile rootProject.ext.awaitility

    testCompile rootProject.ext.junit
}

apply plugin: 'com.google.gms.google-services'
