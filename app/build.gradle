buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
        classpath 'com.google.gms:google-services:3.0.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'idea'

android {

    compileSdkVersion 25
    buildToolsVersion "25"
    defaultConfig {
        applicationId "com.didekindroid"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 2
        versionName "a-0.3.2.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "es" /* Añadir nuevos idiomas según hagan falta.*/
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    //noinspection GroovyAssignabilityCheck
    signingConfigs {
        release {
            if (project.hasProperty('keyalias')) {
                keyAlias keyalias
            }
            if (project.hasProperty('keypassword')) {
                keyPassword keypassword
            }
            if (project.hasProperty('keystore')) {
                storeFile file(keystore)
            }
            if (project.hasProperty('storepassword')) {
                storePassword storepassword
            }
        }
    }

    buildTypes {
        release {
            applicationIdSuffix ""
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            versionNameSuffix '-DEBUG'
            applicationIdSuffix ""
        }
    }

    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "1g"
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError false
        quiet true
        ignoreWarnings false
        htmlReport true
    }
    productFlavors {
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support:support-annotations:25.0.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
    androidTestCompile 'com.android.support.test.espresso:espresso-idling-resource:2.2.2'
    /* Esta librería es para probar en terminales las funcionalidades servidor relacionadas con Firebase GCM y JavaMail*/
    androidTestCompile ':client_gcm_lib-a-0.1.2:@jar'

    compile ':client_lib-a-0.1.2:@jar'
    compile 'com.jakewharton.timber:timber:4.3.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'

    //    compile 'com.android.support:support-annotations:24.2.1'
    compile 'com.android.support:design:25.0.0'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.android.support:recyclerview-v7:25.0.0'
    compile 'com.android.support:gridlayout-v7:25.0.0'
    compile 'com.google.firebase:firebase-messaging:9.4.0'
}

apply plugin: 'com.google.gms.google-services'