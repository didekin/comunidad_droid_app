apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.didekindroid"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 6
        versionName "a-0.6.10"
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
        resConfigs "es" /* , "en"  Añadir nuevos idiomas según hagan falta.*/
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }

    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
    }

    //noinspection GroovyAssignabilityCheck
    signingConfigs {
        release {
            if (project.hasProperty('keyalias')) {
                keyAlias keyalias
            }
            if (project.hasProperty('keypassword')) {
                keyPassword keypassword
            }
            if (project.hasProperty('keystore')) {
                storeFile file(keystore)
            }
            if (project.hasProperty('storepassword')) {
                storePassword storepassword
            }
        }
    }

//    testBuildType "debugPre"    -- Activar antes de pasar a PRE.

    buildTypes {
        release {
            applicationIdSuffix ""
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue "string", "didekin_web_host", '"https://didekin-web-pro.eu-central-1.elasticbeanstalk.com"'
            resValue "string", "didekin_web_port", '":443"'
        }
        pre {
            initWith release
            resValue "string", "didekin_web_host", '"https://didekin-web-pre.eu-central-1.elasticbeanstalk.com"'
            resValue "string", "didekin_web_port", '":443"'
        }
        local {
            initWith release
            resValue "string", "didekin_web_host", '"https://didekinspring.pagekite.me"'
            resValue "string", "didekin_web_port", '""'
        }
        debugPre {
            initWith debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-debug-rules.pro'
            // Variables for android_studio default run-debug configuration.
            if ("$System.env.TEST_HOST" == "null" || "$System.env.TEST_PORT" == "null") {
                // AWSPRE url's
                resValue "string", "didekin_web_host", '"https://didekin-web-pre.eu-central-1.elasticbeanstalk.com"'
                resValue "string", "didekin_web_port", '":443"'
            } else {
                // Variables defined in buildtest.sh environments.
                resValue "string", "didekin_web_host", "$System.env.TEST_HOST"
                resValue "string", "didekin_web_port", "$System.env.TEST_PORT"
            }
            matchingFallbacks = ['debug']
        }
        debug {
            debuggable true
            versionNameSuffix ''
            // Variables for android_studio default run-debug configuration.
            if ("$System.env.TEST_HOST" == "null" || "$System.env.TEST_PORT" == "null") {
                // LOCAL url's
                resValue "string", "didekin_web_host", '"https://didekinspring.pagekite.me"'
                resValue "string", "didekin_web_port", '""'
            } else {
                // Variables defined in buildtest.sh for local and awspre environments.
                resValue "string", "didekin_web_host", "$System.env.TEST_HOST"
                resValue "string", "didekin_web_port", "$System.env.TEST_PORT"
            }
        }
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError true
        quiet true
        ignoreWarnings false
        htmlReport true
//        disable  "InvalidPackage"
    }
    productFlavors {
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        pickFirst 'protobuf.meta'
    }
}

dependencies {
//    TODO: cambiar a release y dejar esta dependencia para debug.
    implementation rootProject.ext.didekindroidlib_one_debug
/*    debugImplementation rootProject.ext.didekindroidlib_one_debug*/

    implementation rootProject.ext.firebase_msg
    implementation rootProject.ext.didekinlib_model
    implementation rootProject.ext.didekinlib_http
    implementation rootProject.ext.timber
    runtimeOnly rootProject.ext.retrofit2
    implementation rootProject.ext.rxjava
    implementation rootProject.ext.android_support
    androidTestImplementation rootProject.ext.didekinlib_gcm
    androidTestImplementation rootProject.ext.testRuler
    androidTestImplementation rootProject.ext.testRunner
    androidTestImplementation rootProject.ext.espresso
    androidTestImplementation rootProject.ext.awaitility
    androidTestUtil rootProject.ext.testOrchestrator
    testImplementation rootProject.ext.junit
    androidTestImplementation rootProject.ext.junit

}

apply plugin: 'com.google.gms.google-services'
